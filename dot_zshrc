#!/bin/zsh
test -f ~/.zshrc_before && source ~/.zshrc_before

# Helper function for configuration warnings
function zsh_config_warn() {
  print -P "%F{yellow}⚠ Warning:%f $1"
}

function zsh_config_error() {
  print -P "%F{red}✗ Error:%f $1"
}

# Configure Homebrew Cask to install to ~/Applications (no sudo for updates)
export HOMEBREW_CASK_OPTS="--appdir=${HOME}/Applications"

# Source profile for shell-agnostic PATH and environment setup
# This must come early so brew shellenv sets FPATH before compinit runs
if [[ -f "$HOME/.profile" ]]; then
  source "$HOME/.profile"
else
  zsh_config_warn "~/.profile not found - PATH may not be fully configured"
fi

# shell and globbing options
set extended_glob
set numeric_glob_sort
set glob_dots
set nomatch

set autocd
set autopushd

export CDPATH=".:~"

set correct

# jobs
set notify

# History configuration

HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=9000

setopt INC_APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS


# Add custom completion directory
fpath=(~/.zsh/completion $fpath)

# Set up completion system with optimized initialization
autoload -Uz compinit

# Only regenerate the completion dump once a day
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# Set up completion system styles
zstyle ':completion:*' add-space true
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _ignored _match _approximate _correct _prefix
zstyle ':completion:*' completions 1
zstyle ':completion:*:descriptions' format '%B%d%b'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd .. directory
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' match-original both
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' max-errors 3
zstyle ':completion:*:default' menu yes select
zstyle ':completion:*' original true
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' prompt 'Got %e, did you mean:'
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' verbose true

zstyle ':completion:*:-command-:*:(commands|builtins|reserved-words|aliases)' group-name commands
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:messages' format %d
zstyle ':completion:*:corrections' format '%B%d (err: %e)%b'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:*:*:(cd|ls):*' ignored-patterns '\.svn'

# Set up vi keybinds
# probably good to do this before the plugins load so any keybinds are in the right keymap

bindkey -v

autoload -Uz edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line


# Plugin manager - Sheldon
# Install sheldon
# Check if sheldon is installed
if (( ! $+commands[sheldon] )); then

  mkdir -p ~/.local/bin
  curl --proto '=https' -fLsS https://rossmacarthur.github.io/install/crate.sh \
    | bash -s -- --repo rossmacarthur/sheldon --to ~/.local/bin

fi

# Start sheldon, init plugins
if (( $+commands[sheldon] )); then
  eval "$(sheldon source)"
else
  zsh_config_error "sheldon not found - plugins will not be loaded"
fi

# Aliases/functions

alias df='df -h'
alias du='du -h'
# sometimes tmux servers get stuck, this can either recreate the socket or otherwise become interactive again
alias fixtmux='killall -s SIGUSR1 tmux'
alias gitlog='git log --graph --decorate --oneline $(git rev-list -g --all)'

# Lazy-load GitHub Copilot
function copilot_init() {
  # Remove placeholder functions
  unfunction ghcs
  unfunction ghce
  unfunction copilot_init
  
  # Load the real GitHub Copilot aliases
  eval "$(gh copilot alias -- zsh)"
}

# Placeholder functions that will trigger copilot initialization
function ghcs() {
  copilot_init
  ghcs "$@"  # Call the real function now that it's loaded
}

function ghce() {
  copilot_init
  ghce "$@"  # Call the real function now that it's loaded
}

# always use the right gradle
function gr {
  if [ -x "gradlew" ]; then
    ./gradlew $@
  else
    gradle $@
  fi
}

if (( $+commands[eza] )); then
  alias tree='eza -T'
fi

# setup the better cd, z
if (($+commands[zoxide] )) then;
    eval "$(zoxide init zsh)"
else
    # Initialize fasd if it's present
    if (( $+commands[fasd] )) then;
      eval "$(fasd --init auto)"
    else
      zsh_config_warn "zoxide and fasd not found - enhanced directory navigation not available"
    fi

fi

function loadOpenRouterKey {
  export OPENROUTER_API_KEY="$(op read "op://Personal/OpenRouter/Aider")"
}
# Setup fzf
# Must occur after completion has started
# ---------
#
function init_fzf() {

local PREFIX="${HOMEBREW_DIR:-/opt/homebrew}"

if [[ -d "$PREFIX/opt/fzf/bin" ]]; then
    if [[ ! "$PATH" == *$PREFIX/opt/fzf/bin* ]]; then
      export PATH="$PATH:$PREFIX/opt/fzf/bin"
    fi

    # Auto-completion
    # ---------------
    [[ $- == *i* ]] && source "$PREFIX/opt/fzf/shell/completion.zsh" 2> /dev/null

    # Key bindings
    # ------------
    source "$PREFIX/opt/fzf/shell/key-bindings.zsh"

    if [[ -f "$(which fd)" ]]; then
        export FZF_DEFAULT_COMMAND="fd --type f --hidden --follow --exclude .git"
        export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    elif [[ -f "$(which rg)" ]]; then 
        export FZF_DEFAULT_COMMAND="rg -g '' --files"
        export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    fi

    if [[ -f "$(which highlight)" ]]; then
        export FZF_CTRL_T_OPTS="--preview '(highlight -O ansi -l {} 2> /dev/null || cat {} || tree -C {}) 2> /dev/null | head -200'"
    else
        export FZF_CTRL_T_OPTS="--preview"
    fi

    # "fix" the alt+c shortcut even when option is in full mac os mode
    bindkey "ç" fzf-cd-widget
else
    zsh_config_warn "fzf not found - fuzzy finder features not available"
fi
}

# zvm => zsh-vi-mode, in which case this should run in some hooks at the end
if [[ -v zvm_after_init_commands ]]; then
    zvm_after_init_commands+=(init_fzf)
else
    init_fzf
fi

# TODO: how to add a github api token in a reasonable way? is it available via
# api? might be an extension from one of the env setup dealies.  Other thoughts
# - store it in the keychain, retrieve it from there?  I could also store them
# in 1password and use that to add it to the keychain

LS_COLORS='no=00:fi=00:di=01;33:ln=01;36:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:su=37;41:sg=30;43:tw=30;42:ow=34;42:st=37;44:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.gz=01;31:*.bz2=01;31:*.deb=01;31:*.rpm=01;31:*.jar=01;31:*.jpg=01;35:*.jpeg=01;35:*.gif=01;35:*.bmp=01;35:*.pbm=01;35:*.pgm=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.tiff=01;35:*.png=01;35:*.mov=01;35:*.mpg=01;35:*.mpeg=01;35:*.avi=01;35:*.fli=01;35:*.gl=01;35:*.dl=01;35:*.xcf=01;35:*.xwd=01;35:*.flac=01;35:*.mp3=01;35:*.mpc=01;35:*.ogg=01;35:*.wav=01;35:';
export LS_COLORS=$LS_COLORS

# attempt to fix emacsclient having wonky colors when launched in terminal
export COLORTERM=truecolor

if (( $+commands[nvim] )) then;
  alias vim=nvim
  alias vi=nvim
  EDITOR=nvim
else
  if (( $+commands[vim] )) then;
    alias vi=vim
    EDITOR=vim
  else
    zsh_config_error "neither nvim nor vim found - EDITOR not set"
    EDITOR=vi  # fallback to system vi
  fi
fi


export EDITOR=$EDITOR
export VISUAL=$EDITOR

if (( $+commands[bat] )); then
  export PAGER=bat
else
  if (( $+commands[less] )); then
    export PAGER=less
  else
    zsh_config_error "neither bat nor less found - PAGER not set"
    export PAGER=more  # fallback to system more
  fi
fi

# connect to daemon in terminal, or start daemon if not running
# alias eb="emacsbare"
# alias emacsbare="emacs -nw -Q --eval \"(load-theme 'misterioso)\""
if [[ ${INSIDE_EMACS:-no_emacs_here} != 'no_emacs_here' ]]; then
  export EDITOR=emacsclient
  export VISUAL=emacsclient

  alias magit="emacsclient -ne '(magit-status)'"

  function man() { emacsclient -ne "(man \"$1\")"; }
  alias e="emacsclient"
else
  alias e="emacsclient -t -a ''"
fi

# JAVA_HOME is primarily managed by mise
# Fallback to macOS java_home if mise-managed Java not available
if [[ -z "$JAVA_HOME" ]] && [[ -x "/usr/libexec/java_home" ]]; then
  export JAVA_HOME="$(/usr/libexec/java_home 2>/dev/null)" || true
fi

# Copilot aliases are now lazy-loaded above


# Add doom to path
export PATH="$HOME/.emacs.d/bin:$PATH"

# Initialize starship prompt
if (( $+commands[starship] )); then
  eval "$(starship init zsh)"
else
  zsh_config_error "starship not found - using default prompt"
fi

# Initialize mise for tool version management
if (( $+commands[mise] )); then
  eval "$(mise activate zsh)"
else
  zsh_config_warn "mise not found - development tool versions may not be available"
fi

# Set up br alias for broot if available
[[ -s "$HOME/.config/broot/launcher/bash/br" ]] && source "$HOME/.config/broot/launcher/bash/br"

# Source a local zshrc if found (though that is better managed with profiles)
test -f ~/.zshrc_local && source ~/.zshrc_local
